buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
        classpath 'com.noveogroup.android:check:1.2.3'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.1'
    }
}
def buildNumber = BUILD_NUM.toInteger()

apply plugin: 'com.android.application'
apply plugin: 'com.noveogroup.android.check'
apply plugin: 'io.fabric'
apply plugin: 'jacoco-android'

check {
    abortOnError true
    pmd { skip true }
    findbugs { skip true }
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        applicationId "com.multunus.onemdm"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode buildNumber
        versionName "1.0."+buildNumber
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    def releaseBuild = true;

    signingConfigs {
        release {
            try {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("STORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
            catch (Exception ex){
                releaseBuild = false;
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if(releaseBuild) {
                signingConfig signingConfigs.release
            }
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def buildTypeName = ""
                    if(variant.buildType.name == "debug"){
                        buildTypeName = "-debug"
                    }
                    output.outputFile = new File(
                            output.outputFile.parent,
                            "onemdm${buildTypeName}-v${variant.versionName}.apk")
                }
            }
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.0.1'
    compile 'com.google.android.gms:play-services:8.1.0'
    compile 'com.mcxiaoke.volley:library:1.0.16'
    compile 'com.google.code.gson:gson:2.4'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.2@aar') {
        transitive = true;
    }
    compile 'io.realm:realm-android:0.87.2'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:1.7.0'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-all:1.8.4'
    testCompile 'io.reactivex:rxjava:1.1.0'
}

